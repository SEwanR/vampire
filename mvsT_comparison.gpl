#!/bin/bash
#-------------------------------------------------------------------------------
#      Sample gnuplot template for plotting magnetization m(T) curve
#
#                (c) R F L Evans 2016. All rights reserved.
#
#-------------------------------------------------------------------------------
# Load styles
load "../../journals/nature.journal"
load "../../colours/bluegold-sl.colour"

# Set output file base name
ofile="mvsT"

# Set axis labels
set xlabel "Temperature (K)"
# For y-label use italics for variables and offset from axis
set ylabel "Magnetisation ({/Helvetica-Oblique m}/{/Helvetica-Oblique m}_s)" offset 0.5,0

# Set axis number formatting
set format y "%3.1f"

# Set '0' line as guide to the eye
set xzeroaxis

# Set key properties
set key top right maxrows 5 spacing 1.2

#-------------------------------------------------------------------------------
# Fitting function  m(T) = (1 - T/Tc) ^ beta
# For T > Tc equation is not valid, so use a ternary operator
# (bool) ? value_if_true : value_if_false
m(T)= T<Tc ? (1-T/Tc)**beta : 0.0
# Estimate initial variable
Tc=619.1
beta=0.329
#-------------------------------------------------------------------------------

# set columns to be fitted here T,|m|
fit [] m(x) "output_FePt_LLG" u 1:2 via Tc, beta
fit [] m(x) "output_FePt_LSF_corrected" u 1:2 via Tc, beta
fit [] m(x) "output_FePt_MC" u 1:2 via Tc, beta
fit [] m(x) "output" u 1:($2/0.879254) via Tc, beta

# Print fitted values to screen
print "beta =",beta
print "Tc =  ",Tc

# Set samples for fiitted function for smoother plot
set samples 1000

# Set eps file name by concatenation (. operator)
epsfile=ofile.".eps"
set output epsfile

#--------------------------------------------
# Plot graph with fit
p [-20:800][-0.05:1.1] \
"output_FePt_LLG" u 1:2 ev 4 ls 2 ps 2 w p title "LLG",\
"output_FePt_LLG" u 1:2 ev 4 ls 2 lw 4 w l notitle, \
"output_FePt_LSF_corrected" u 1:2 ev 4 ls 3 ps 2 w p title "LSF-I",\
"output_FePt_LSF_corrected" u 1:2 ev 4 ls 3 lw 4 w l notitle, \
"output" u 1:($2/0.879254) ev 4 ls 4 ps 2 w p title "LSF-II",\
"output" u 1:($2/0.879254) ev 4 ls 4 lw 4 w l notitle, \
"output_FePt_MC" u 1:2 ev 4 ls 5 ps 2 w p title "Monte Carlo", \
"output_FePt_MC" u 1:2 ev 4 ls 5 lw 4 w l notitle
#m(x) w l ls 101 lw 4 ti "Fit"

# Convert eps to pdf for portability
cmd = "epstopdf ".epsfile
sys cmd
